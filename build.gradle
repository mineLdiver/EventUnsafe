plugins {
    id 'java'
    id 'maven-publish'
    id "io.freefair.lombok" version "8.0.1"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

group 'net.mine_diver'
version '1.0'

repositories {
    mavenCentral()
    maven {
        name = "Jitpack"
        url 'https://jitpack.io/'
    }
    maven {
        url 'https://maven.minecraftforge.net/'
    }
}

sourceSets {
    jmh {
        java.srcDirs = ['src/jmh/java']
        resources.srcDirs = ['src/jmh/resources']
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    implementation 'org.ow2.asm:asm:9.3'
    implementation 'org.ow2.asm:asm-analysis:9.3'
    implementation 'org.ow2.asm:asm-commons:9.3'
    implementation 'org.ow2.asm:asm-tree:9.3'
    implementation 'org.ow2.asm:asm-util:9.3'

    implementation "net.jodah:typetools:0.8.+"

    implementation 'com.google.guava:guava:31.1-jre'

    implementation 'it.unimi.dsi:fastutil:8.5.8'

    implementation 'org.jetbrains:annotations:23.0.0'

    jmhImplementation 'org.openjdk.jmh:jmh-core:1.35'
    jmhImplementation 'org.openjdk.jmh:jmh-generator-annprocess:1.35'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.35'
}

test {
    useJUnitPlatform()
}

task jmh(type: JavaExec, dependsOn: jmhClasses) {
    mainClass = 'org.openjdk.jmh.Main'
    classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath
}

classes.finalizedBy(jmhClasses)

publishing {
    repositories {
        if (project.hasProperty("glass_maven_username")) {
            maven {
                url = "https://maven.glass-launcher.net/releases"
                credentials {
                    username = "${project.properties["glass_maven_username"]}"
                    password = "${project.properties["glass_maven_password"]}"
                }
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = rootProject.name
            version = version

            from components.java
        }
    }
}